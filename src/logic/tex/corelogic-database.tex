\section{Beispiel Datenbanken}

\subsection{Hintergrund}

Bei der funktionalen Programmierung und/oder auch der Relationalen, bietet es sich an, über eine Faktenbasis und deren verschiedenen Relationen eine Art Datenbank zu erstellen um dort Informationen abfragen zu können. Mit core.logic ist so etwas relativ schnell und komfortabel mit dem Unterpacket von core.logic, core.logic.pldb möglich.

\subsection{Model}

Unser Model ist eine gewöhnliche italienische Familie. Dort exisiteren Väter, Mütter und Kinder. Ein Vater hat ein Kind und eine Mutter hat ein Kind. Das sind dann auch unsere Relationen.

\begin{lstlisting}
Relation V : X ist Vater von Y

Relation M : X ist Mutter von Y
\end{lstlisting}

\subsection{Code}

Die Packete \code{core.logic} und hauptsächlich \code{core.logic.pldb} mit folgendem Befehl bekannt machen, dabei aber das \code{==} vom clojure.core nicht berücksichtigen.

\begin{lstlisting}
(ns logic.core	
	(:refer-clojure :exclude [==])
	(:use [clojure.core.logic])
  	(:use [clojure.core.logic.pldb]))
\end{lstlisting}

Definieren der oben formulierten Relationen im Programm. Hierbei werden diese an ein Symbol und damit an ein Objekt im Speicher gebunden.

\begin{lstlisting}
(db-rel father Father Child)
(db-rel mother Mother Child)
\end{lstlisting}

Die oben programmintern gebundenen Relationen können jetzt verwendet werden um mit \code{db} eine Repräsentation im Speicher zu erzeugen und diese wieder an Symbole zu binden. Dabei bekommt \code{db} eine Anzahl von Vektoren, die am Anfang ein Relations-Objekt (\dq{}Instanz\dq{} von db-rel) stehen haben und danach Argumente von der Größe des Tupel der Relation folgen um die Relation und des Tupel darzustellen.

\begin{lstlisting}
(def dbf (db [father 'Vito 'Michael]
             [father 'Vito 'Sonny]
             [father 'Vito 'Fredo]
             [father 'Michael 'Anthony]
             [father 'Michael 'Mary]
             [father 'Sonny 'Vicent]
             [father 'Sonny 'Francesca]
             [father 'Sonny 'Kathryn]
             [father 'Sonny 'Frank]
             [father 'Sonny 'Santino]))
 
(def dbm (db [mother 'Carmela 'Michael]
             [mother 'Carmela 'Sonny]
             [mother 'Carmela 'Fredo]
             [mother 'Kay 'Mary]
             [mother 'Kay 'Anthony]
             [mother 'Sandra 'Francesca]
             [mother 'Sandra 'Kathryn]
             [mother 'Sandra 'Frank]
             [mother 'Sandra 'Santino]))
\end{lstlisting}

Der Zugriff auf die Tabellen unseres realtionalen Datenbankmodels erfolgt über \code{with-db} auf eine Datenbank und über \code{with-dbs} auf einen Vektor von Datenbanken. Der Aufruf des Solvers erfolgt nach altbekanntem Muster. Die Benutzung von code{conde} bietet sich in diesem Fall an, da wir so mehrere Filter verodern können. In diesem Fall entspricht das einem SQL-ähnlichen \code{JOIN} auf das Kind q.

\begin{lstlisting}
(defn children [p] 
  "Returns a list of children of p"
  (with-dbs [dbf dbm] 
	(run* [q] 
		(conde ((father p q)) ((mother p q))))))
\end{lstlisting}

Somit bekommen wir mit folgendem Code folgende Ausgabe:

\begin{lstlisting}
(children 'Vito)
; => (Michael Sonny Fredo)
\end{lstlisting}

Dabei funktioniert nur das Symbol \code{'Vito} und nicht der String \code{"Vito"}

\begin{lstlisting}
(children "Vito")
; => ()
\end{lstlisting}

Ein ähnliches Vorgehen für die Ermittlung der Eltern eines Kindes \dq{}c\dq{}. Was vielleicht verwirrt, das wir bei dem Suffix der Funktion relativ willkürlich ein e gewählt haben. Siehe Kapitel d. Höhere Funktionen in III. Syntax von core.logic.

\begin{lstlisting}
(defn parentse [c] 
  "Returns a list of parents of c"
  (with-dbs [dbm dbf] 
	(run* [q] 
		(conde ((father q c)) ((mother q c))))))
\end{lstlisting}

Wird nichts gefunden bekommen wir eine Leere Menge.

\begin{lstlisting}
(parentse 'Vito)
; => ()

(parentse 'Anthony)
; => (Michael Kay)
\end{lstlisting}

Das Ermitteln des Vaters bzw. der Mutter ist sehr ähnlich. Und nur deshalb nur der Vollständigkeit halber erwähnenswert.

\begin{lstlisting}
(defn fathero [c]
  "Returns the father of c"
  (with-db dbf 
	(run* [q] 
		(father q c))))

(defn mothero [c]
  "Returns the mother of c"
  (with-db dbm (run* [q] (mother  q c))))
\end{lstlisting}

Bei der Ermittlung der Großenkel eines Großvaters wird es schon etwas komplizierter. Wir möchten alle Enkel in der Abfrage erwischen, das sind anders gesagt, die Kinder der Kinder. Genauer: Wir legen uns per \code{fresh} zwei neue logische Variablen \code{x} und \code{y} an. Unser gegebenes \code{g} ist der Großvater und damit der Anfang unserer \dq{}verschlungenen\dq{} Relationenkette. Jedes q aus der Menge aller Kinder der \code{dbf} Tabelle, welches einen Vater \code{x} hat, welcher \code{g} seinen Vater nennt, wird in die Lösungsmenge aufgenommen. In PostgreSQL wäre so etwas mit \code{RECURSIVE} möglich oder auch mit einem code{JOIN} auf einer Tabelle.

\begin{lstlisting}
(defn grandchild [g]
  "Returns the grandchild(s) of g"
  (with-dbs [dbf] (run* [q] (fresh [x y] (father g x) (father x y) (== q y)))))
\end{lstlisting}

Damit bekommen wir mit der Eingabe \code{'Vito}, alle Enkel von ihm.

\begin{lstlisting}
(grandchild 'Vito)
; (Francesca Santino Frank Mary Kathryn Vicent Anthony)
\end{lstlisting}